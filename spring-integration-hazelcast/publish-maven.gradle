apply plugin: 'maven'

ext.optionalDeps = []
ext.providedDeps = []

ext.optional = { optionalDeps << it }
ext.provided = { providedDeps << it }

install {
	repositories.mavenInstaller {
		customizePom(pom, project)
	}
}

def customizePom(pom, gradleProject) {
	pom.whenConfigured { generatedPom ->

		// sort to make pom dependencies order consistent to ease comparison of older poms
		generatedPom.dependencies = generatedPom.dependencies.sort { dep ->
			"$dep.scope:$dep.groupId:$dep.artifactId"
		}

		def managedVersions = dependencyManagement.managedVersions
		generatedPom.dependencies.findAll{dep -> !dep.version }.each { dep ->
			dep.version = managedVersions["${dep.groupId}:${dep.artifactId}"]
		}

		// respect 'optional' and 'provided' dependencies
		gradleProject.optionalDeps.each { dep ->
			generatedPom.dependencies.find { it.artifactId == dep.name }?.optional = true
		}
		gradleProject.providedDeps.each { dep ->
			generatedPom.dependencies.find { it.artifactId == dep.name }?.scope = 'provided'
		}

		// eliminate test-scoped dependencies (no need in maven central poms)
		generatedPom.dependencies.removeAll { dep ->
			dep.scope == 'test'
		}

		// add all items necessary for maven central publication
		generatedPom.project {
			name = gradleProject.description
			description = gradleProject.description
			url = 'https://github.com/spring-projects/spring-integration-aws'
			organization {
				name = 'SpringIO'
				url = 'https://spring.io'
			}
			licenses {
				license {
					name 'The Apache Software License, Version 2.0'
					url 'https://www.apache.org/licenses/LICENSE-2.0.txt'
					distribution 'repo'
				}
			}
			scm {
				url = 'https://github.com/spring-projects/spring-integration-aws'
				connection = 'scm:git:git://github.com/spring-projects/spring-integration-aws'
				developerConnection = 'scm:git:git://github.com/spring-projects/spring-integration-aws'
			}

			developers {
				developer {
					id = 'amolnayak311'
					name = 'Amol Nayak'
					email = 'amolnayak311@gmail.com'
				}
				developer {
					id = 'ghillert'
					name = 'Gunnar Hillert'
					email = 'ghillert@pivotal.io'
				}
				developer {
					id = 'abilan'
					name = 'Artem Bilan'
					email = 'abilan@pivotal.io'
				}
			}
		}
	}
}
